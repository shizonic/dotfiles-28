if ($?USER == 0 || $?prompt == 0) then
	exit
endif

set     red="%{\033[1;31m%}"
set   green="%{\033[0;32m%}"
set  yellow="%{\033[1;33m%}"
set    blue="%{\033[1;34m%}"
set magenta="%{\033[1;35m%}"
set    cyan="%{\033[1;36m%}"
set   white="%{\033[0;37m%}"
set     end="%{\033[0m%}" # This is needed at the end... :(

umask 077

setenv EDITOR vim
setenv PAGER less

# Abriviate pathes by "..."
set ellipsis

# End character for root (>) and unprivileged users (:)
set promptchars=":>"

# Left prompt (user@host:pty path:)
#set prompt="%n@%m:%l %c02%# "
set prompt="${green}%n${blue}@%m ${white}%~ ${green}%%${end} "

# Right prompt ([hour:min:sec])
set rprompt="[%P]"

# Prompt level 2 (multiline editing, e.g loops)
set prompt2="%R?"

# Prompt level 2 (autocorrections)
set prompt3="CORRECT> %B%R%b (y|n|e)?"

# History length
set history=10000

# Save 4096 lines to hard disk and merge the in
# memory history with the existing hard copy.
set savehist=( 4096 merge lock )

# File to save the history to
set histfile=~/.tcsh/run/history

# Do not save duplicate commands
set histdup=erase


# File to save to
set dirsfile=~/.tcsh/run/dirstack

# History up and down on "up" and "down"
# cursor keys. This is the default on
# FreeBSD, but not on Linux.
bindkey -k up history-search-backward
bindkey -k down history-search-forward

# Delete on 'del'
bindkey "\e[3~" delete-char-or-eof

# Search history on ctrl-r
bindkey ^r i-search-back
bindkey ^R i-search-fwd

# Autolisting of options only when
# no new characters could be added
set autolist=ambiguous

# Autocorrection, but only for
# commands and nothing else
set autocorrect
set correct=cmd

# Use the command history to find
# completions
set autoexpand

# Never ever autologout
unset autologout

# Automatic rebuild the path hashtable.
# (no more "rehash" necessary with this)
set autorehash=always

# Enables colored output for embedded commands
set color

# Enables color setting via escape sequences
# (Those must be supported by the terminal)
set colorcat

# Autocompletion
set complete

# Let 'pushd' remove all instances of the pushed
# directory of the stack before pushing
set dunique

# Support both BSD and SysV echo style
set echo_style=both

# * matches files starting with a .
set globdot

# Allow the wildcard '**' to recurse into subdirectories
set globstar

# List all jobs after ctrl-z
set listjobs=long

# Never ever beep
set nobeep

# Hours are always printed with
# 2 digits (07 instead of 7)
set padhour

# Print exitcode if it's non-zero
set printexitvalue

# pushd without arguments changed to $HOME
set pushdtohome

# Ask before "rm *". Can be overriden with "-f"
set rmstar

# Chase symlinks
set symlinks=chase

# Display 'time' for commands that took
# longer than 5 seconds (Smaller values
# may lead to problems with subshells
# used for completions)
set time=5

setenv CLICOLOR 1
setenv LSCOLORS "exfxcxdxbxexexaxaxaxax"
